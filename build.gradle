plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

group = 'dev.rollczi'
version = '2.0.2'

repositories {
    mavenCentral()
    maven { url "https://repo.panda-lang.org/releases" }
    maven { url "https://papermc.io/repo/repository/maven-public/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }

    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url "https://repo.mattstudios.me/artifactory/public/" }
}

dependencies {
    // Spigot API
    compileOnly 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'

    // kyori adventure
    implementation 'net.kyori:adventure-platform-bukkit:4.1.2'
    implementation 'net.kyori:adventure-text-minimessage:4.12.0'

    // LiteCommands
    implementation "dev.rollczi.litecommands:core:1.8.4"
    implementation "dev.rollczi.litecommands:bukkit:1.8.4"

    // TriumphGui
    implementation 'dev.triumphteam:triumph-gui:3.1.3'

    // CDN
    implementation 'net.dzikoysk:cdn:1.13.5'
}

compileJava.options.encoding = "UTF-8"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveFileName = "LiteItemVoid-${project.version}-build.jar"

    exclude "org/intellij/lang/annotations/**"
    exclude "org/jetbrains/annotations/**"
    exclude "javax/**"

    relocate "net.dzikoysk", "dev.rollczi.liteitemvoid.libs.net.dzikoysk"
    relocate "dev.rollczi.litecommands", "dev.rollczi.liteitemvoid.libs.dev.rollczi.litecommands"
    relocate "dev.triumphteam", "dev.rollczi.liteitemvoid.libs.dev.triumphteam"
    relocate "com.google.gson", "dev.rollczi.liteitemvoid.libs.com.google.gson"
    relocate "net.kyori", "dev.rollczi.liteitemvoid.libs.net.kyori"
    relocate "panda", "dev.rollczi.liteitemvoid.libs.panda"
    relocate "org.panda_lang", "dev.rollczi.liteitemvoid.libs.panda"
}